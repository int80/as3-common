<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   addedEffect="Fade" showEffect="Fade" 
			   width="400" height="300" contentCreationComplete="_setup()" close="_closeHandler(event)"
			   implements="biz.int80.IPersistantStorage">
	
	<fx:Declarations>
		<fx:Boolean id="centered">true</fx:Boolean>
		<fx:Boolean id="closable">true</fx:Boolean>
	</fx:Declarations>
	
	<s:layout>
		<s:BasicLayout/>
	</s:layout>
	
	<fx:Script source="PersistantStorage.as" />
	
	<fx:Script>
		<![CDATA[
			import flash.utils.describeType;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.MoveEvent;
			import mx.managers.PopUpManager;
			
			import spark.components.TitleWindow;
			
			public var resizable:Boolean = true;
			
			private function _setup():void {
				this.loadDimensions();
				
				this.addEventListener(MoveEvent.MOVE, windowMoved);
			}
			
			protected function windowMoved(event:MoveEvent):void {
				if (x + width > parent.width && parent.width - width > 0) x = parent.width - width;
				if (y + height > parent.height && parent.height - height > 0) y = parent.height - height;
				
				if (x < 0) move(0, y);
				if (y < 0) move(x, 0);
			}
			
			public function get sharedObjectPrefix():String {
				return "windowStatev1_";
			}
			
			// load location and dimensions from sharedobject storage
			private function loadDimensions():Boolean {
				var savedState:SharedObject = this.sharedObject;
				if (! savedState) return false;
				
				// load position
				var savedLeft:Number = savedState.data.left;
				var savedTop:Number = savedState.data.top;
				if (! savedLeft && ! savedTop)
					return false;
				if (savedLeft < 0 || savedTop < 0)
					return false;
				this.x = savedLeft;
				this.y = savedTop;
				
				if (! this.resizable) return true;
				
				// load width/height
				var savedWidth:Number = savedState.data.width;
				var savedHeight:Number = savedState.data.height;
				
				// sanity check dimensions
				if (! savedWidth || ! savedHeight)
					return false;
				if (savedWidth < 10 || savedHeight < 10)
					return false;
				
				// force dimensions to be within the current screen size
				var parentWidth:int = this.parentApplication.width;
				var parentHeight:int = this.parentApplication.height;
				if (savedLeft >= parentWidth - 10) return false;
				if (savedTop >= parentHeight - 10) return false;
				if (savedLeft + savedWidth >= parentWidth) return false;
				if (savedTop + savedHeight >= parentHeight) return false;
				
				this.width = savedWidth;
				this.height = savedHeight;
				
				return true;
			}
			
			private function saveDimensions():void {
				var savedState:SharedObject = this.sharedObject;
				if (! savedState) return;

				var bounds:Rectangle = this.getBounds(this.parent);
				
				savedState.data.left = bounds.left;
				savedState.data.top = bounds.top;
				savedState.data.width = bounds.width;
				savedState.data.height = bounds.height;
				savedState.flush();
			}
		
			
			private function _closeHandler(evt:CloseEvent):void {
				saveDimensions();
				
				if (closable)
					postClose();
				else
					evt.preventDefault();
			}
			
			public function postClose():void {
				PopUpManager.removePopUp(this);
			}
		]]>
	</fx:Script>
</s:TitleWindow>
